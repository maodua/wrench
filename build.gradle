plugins {
	id 'java-library'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.5.6'
    id 'maven-publish'
    id 'signing'
}

description = "æ‹§èºä¸çš„å¥½æ‰³æ‰‹"

ext {
    libraries = [
        junitVersion = '5.8.1'
    ]

	lib = [
			"mybatis-plus"				            : 	"com.baomidou:mybatis-plus-boot-starter:3.4.2",
            "pagehelper"                            :   "com.github.pagehelper:pagehelper-spring-boot-starter:1.4.0",
			"lombok"              					:	"org.projectlombok:lombok:1.18.22",
            "mysql-connector-java"                  :   "mysql:mysql-connector-java",
            // JUnit 5 Jupiter API
            "junit-jupiter-api"                     :   "org.junit.jupiter:junit-jupiter-api:${junitVersion}",
            // JUnit 5 çš„æµ‹è¯•å¼•æ“ engine
            "junit-jupiter-engine"                  :   "org.junit.jupiter:junit-jupiter-engine:${junitVersion}",

    ]
}

// æ‰€æœ‰é¡¹ç›®é…ç½®
allprojects {
	group = 'io.github.maodua'
	version = '1.0.0'

	java {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
		withJavadocJar()
		withSourcesJar()
	}

	repositories {
		mavenLocal()
		maven { url "https://maven.aliyun.com/nexus/content/groups/public" }
		maven { url "https://maven.aliyun.com/nexus/content/repositories/gradle-plugin" }
		mavenCentral()
        // spring ä»“åº“
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
	}
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    jar {
        enabled = true
        // jaræ–‡ä»¶åä¸­åˆ é™¤ `plain` åç¼€
        archiveClassifier.set("")
    }
    bootJar {
        enabled = false
    }

    // java ç¼–ç 
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.warnings = false
        options.deprecation = true
        options.compilerArgs += ["-parameters"]
    }

    test {
        // ä½¿ç”¨JUnitå¹³å°
        useJUnitPlatform()
    }

    dependencies {
        api                     'org.springframework.boot:spring-boot-starter'
        annotationProcessor     "org.springframework.boot:spring-boot-configuration-processor"
        compileOnly             "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor     "org.projectlombok:lombok"
        compileOnly             "org.projectlombok:lombok"

        testAnnotationProcessor "org.projectlombok:lombok"
        testCompileOnly         "org.projectlombok:lombok"
        // spring-boot-test ä¸­å·²ç»åŒ…å« junit
        testImplementation      "org.springframework.boot:spring-boot-starter-test"
    }

    // å‘ç‰ˆ
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'wrench'
                    description = 'ğŸ”§ æ‹§èºä¸çš„å¥½æ‰³æ‰‹'
                    url = 'https://github.com/maodua/wrench'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    // å¼€å‘è€…ä¿¡æ¯
                    developers {
                        developer {
                            id = 'sch'
                            name = 'shanchanghui'
                            email = 'changhui.shan@qq.com'
                        }
                    }
                    // SCMé…ç½®
                    scm {
                        connection = 'scm:git:git://github.com/maodua/wrench.git'
                        developerConnection = 'scm:git:ssh://github.com:maodua/wrench.git'
                        url = 'https://github.com/maodua/wrench/tree/main'
                    }
                }
            }
        }
        // ä»“åº“ä¿¡æ¯
        repositories {
            maven {
                def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username ossrh_username
                    password ossrh_password
                }
            }
        }

    }

    // ç­¾å
    signing {
        sign publishing.publications.mavenJava
    }

    javadoc {
        // é˜²æ­¢æœ¬åœ°æ‰“å¼€ä¸­æ–‡ä¹±ç 
        options.addStringOption("charset", "UTF-8")
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

}
